type sandwich =
  | Portabello
  | Ham
  | Unicorn;

type burger = {
  tomatoes: bool,
  lettuce: bool,
  bacon: int,
  onions: int,
  cheese: int,
};

let sandwichToString = sandwich => {
  let sandwich =
    switch (sandwich) {
    | Portabello => {js|ðŸ„|js}
    | Ham => {js|ðŸ·|js}
    | Unicorn => {js|ðŸ¦„|js}
    };
  sandwich ++ " Sandwich";
};

let burgerToString = ({lettuce, tomatoes, bacon, onions, cheese}) => {
  let multi = (str, n) => Array.make(n, str) |> Js.Array.joinWith("");

  let listStr =
    [|
      lettuce ? {js|ðŸ¥¬|js} : "",
      tomatoes ? {js|ðŸ…|js} : "",
      multi({js|ðŸ¥“|js}, bacon),
      multi({js|ðŸ§…|js}, onions),
      multi({js|ðŸ§€|js}, cheese),
    |]
    |> Js.Array.filter(str => str != "")
    |> Js.Array.joinWith(", ");
  {js|ðŸ” (|js} ++ listStr ++ ")";
};

let itemToString = item =>
  switch (item) {
  | `Sandwich(sandwich) => sandwichToString(sandwich)
  | `Burger(burger) => burgerToString(burger)
  };

type standardItem = [ | `Sandwich(sandwich) | `Burger(burger)];

module LunchOrder = {
  type lunchItem = [ standardItem | `Hotdog | `Meatpie];

  [@react.component]
  let make = (~order: array(lunchItem)) =>
    <ul style={ReactDOMStyle.make(~fontSize="2em", ())}>
      {order
       |> Array.mapi((idx, item) => {
            let label =
              switch (item) {
              | `Hotdog => {js|ðŸŒ­|js}
              | `Meatpie => {js|ðŸ¥§|js}
              | #standardItem as item => itemToString(item)
              };
            <li key={"item-" ++ string_of_int(idx)}> {React.string(label)} </li>;
          })
       |> React.array}
    </ul>;
};

module DinnerOrder = {
  type dinnerItem = [ standardItem | `Steak | `Ramen];

  [@react.component]
  let make = (~order: array(dinnerItem)) =>
    <ul style={ReactDOMStyle.make(~fontSize="2em", ())}>
      {order
       |> Array.mapi((idx, item) => {
            let label =
              switch (item) {
              | `Steak => {js|ðŸ¥©|js}
              | `Ramen => {js|ðŸœ|js}
              | #standardItem as item => itemToString(item)
              };
            <li key={"item-" ++ string_of_int(idx)}> {React.string(label)} </li>;
          })
       |> React.array}
    </ul>;
};
